plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'checkstyle'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.hidetake.swagger.generator' version "${swaggerGeneratorVersion}"
}

group = "${projectGroup}"
version = "${applicationVersion}"

jar.enabled = false
java.sourceCompatibility = "${javaVersion}"

apply from: 'gradle/check-style.gradle'
apply from: 'gradle/lombok.gradle'
apply from: 'gradle/test-support.gradle'
apply from: 'gradle/rest-docs.gradle'

dependencies {
    // spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // db
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // jasypt
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'
}

repositories {
    mavenCentral()
}

tasks.named('test') {
    useJUnitPlatform()
}
